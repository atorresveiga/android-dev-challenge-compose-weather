plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlinx-serialization'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.appdistribution'
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.atorresveiga.bluecloud"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 2
        versionName "1.0"
        buildConfigField('String', 'OpenWeatherSecAPIKey', OpenWeatherAPIKey)
        buildConfigField('String', 'OpenWeatherBaseURL', "\"https://api.openweathermap.org/data/2.5/\"")
        buildConfigField('String', 'GeoNamesUser', GeoNamesUser)
        buildConfigField('String', 'GeoNamesBaseURL', "\"http://api.geonames.org/\"")
        buildConfigField('String', 'MetNoContact', MetNoContact)
        buildConfigField('String', 'MetNoBaseURL', "\"https://api.met.no/weatherapi/\"")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    signingConfigs {
        // We use a bundled debug keystore, to allow debug builds from CI to be upgradable
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            firebaseAppDistribution {
                releaseNotes = "Debug version"
                groups = "debug"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "data"

    productFlavors {
        fake {
            dimension "data"
            versionNameSuffix "-fake"
        }
        network {
            dimension "data"
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }

    buildFeatures {
        compose true

        // Disable unused AGP features
        buildConfig true
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        // Multiple dependency bring these files in. Exclude them to enable
        // our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.4.0-alpha03'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.activity:activity-compose:1.3.1"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'


    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.room:room-runtime:2.3.0'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0-alpha03'
    implementation "androidx.navigation:navigation-compose:2.4.0-alpha06"

    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.3.1'

    annotationProcessor 'androidx.room:room-compiler:2.3.0'
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Kotlin Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0"

    // Kotlin Datetime
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.2.1"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    // Datastore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Work Manager
    implementation "androidx.work:work-runtime-ktx:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"
    implementation "androidx.hilt:hilt-work:1.0.0"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // https://github.com/Kotlin/kotlinx.collections.immutable/issues/96 needed to remove jcenter until compose update the dependency
    implementation "org.jetbrains.kotlinx:kotlinx-collections-immutable-jvm:0.3.4"

    // Swipe refresh
    implementation "com.google.accompanist:accompanist-swiperefresh:0.16.0"
    implementation 'com.google.accompanist:accompanist-insets:0.16.0'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:28.3.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // Test helpers
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.1.2"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation "com.google.truth:truth:1.1.2"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"

}
